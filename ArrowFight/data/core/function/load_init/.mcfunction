#> core:load_init/
#
# 
#
# @within function core:load

# ロビーのオブジェクトを削除
    kill @e[tag=AssetObject]
    kill @e[tag=LobbyHologram]

# スコアボード作成
    scoreboard objectives add MobUUID dummy {"text":"汎用固有MobID"}
    scoreboard objectives add ObjectID dummy {"text":"ObjectAssetのID"}
    scoreboard objectives add MobID dummy {"text":"MobAssetのID"}

    #> 1tickで消す一時変数の保存用スコアボード
    # @public
        scoreboard objectives add Temporary dummy

    #> 常に値が設定される変数用スコアボード
    # @public
        scoreboard objectives add Global dummy

    #> 定数類用スコアボード **変更厳禁**
    # @public
        scoreboard objectives add Const dummy
        function core:define_const

    #> AssetManager: Object -Public
    # @within function
    #   asset:object/**
        scoreboard objectives add General.Object.Tick dummy {"text":"ObjectAsset内で使用できるTick用スコア"}

    #> AssetManager: Mob -Public
    # @within function
    #   asset:mob/**
        scoreboard objectives add General.Mob.Tick dummy {"text":"MobAsset内で使用できるTick用スコア"}

    #> 試合用スコア
    # @within function
    #   world_manager/event/
        scoreboard objectives add Game.Tick dummy {"text":"試合のTick用スコア"}
        scoreboard objectives add DeathMatch.Tick dummy {"text":"試合のTick用スコア"}

    #> MobManager用スコアボード - 攻撃元
    # @within function
    #   core:early_handler/damage
    #   asset_manager:artifact/handler/on_damage_without_source
    #   mob_manager:entity_finder/entity_hurt_player/fetch_entity
        scoreboard objectives add TakenDamage custom:damage_taken
        scoreboard objectives add AbsorbedDamage custom:damage_absorbed
        
    #> UserID
    # @public
        scoreboard objectives add UserID dummy {"text":"汎用固有ユーザーID"}
        scoreboard objectives add MobUUID dummy {"text":"汎用固有MobID"}
        
    #> Library用スコアボード
    # @public
        scoreboard objectives add Lib dummy {"text":"ライブラリの引数/返り値用"}
        scoreboard objectives add ForwardTargetMobUUID dummy {"text":"ダメージを転送する対象"}

    #> イベントハンドラ用スコアボード
    # @within function
    #   asset_manager:artifact/triggers/**
    #   core:load_once
    #   core:handler/*
    #   core:tick/**
        scoreboard objectives add FirstJoinEvent custom:play_time {"text":"イベント: 初回Join"}
        scoreboard objectives add RejoinEvent custom:leave_game {"text":"イベント: ゲームから退出"}
        scoreboard objectives add DeathEvent deathCount {"text":"イベント: 死亡"}
        scoreboard objectives add RespawnEvent custom:time_since_death {"text":"イベント: リスポーン"}
        scoreboard objectives add Sneak custom:sneak_time {"text":"イベント: スニーク"}
        #scoreboard objectives add Elytra custom:aviate_one_cm {"text":"イベント: エリトラ"}
        #scoreboard objectives add DropEvent custom:drop {"text":"イベント: アイテムドロップ"}
        #scoreboard objectives add ClickCarrotEvent used:carrot_on_a_stick {"text":"イベント: クリック 人参棒"}
        scoreboard objectives add UseBowEvent used:bow {"text":"イベント: 弓の使用"}

    #> トリガー
        scoreboard objectives add Trigger.DialogShop trigger

    #> その他スコアボード
        scoreboard objectives add StopRegeneration dummy {"text":"ヘルス再生停止"}
        scoreboard objectives add DelayLoad dummy {"text":"遅延ロード"}

    #> ally.golem: 被害者リンクおよびキル帰属
    # @within function
    #   asset:mob/ally.golem/**
        scoreboard objectives add ally.golem.VictimLink dummy {"text":"ally.golem: 被害者のUserID"}
        scoreboard objectives add ally.golem.ShooterLink dummy {"text":"ally.golem: 射手のUserID"}
        scoreboard objectives add ally.golem.PosX dummy {"text":"ally.golem: ゴーレムのX座標"}
        scoreboard objectives add ally.golem.PosY dummy {"text":"ally.golem: ゴーレムのY座標"}
        scoreboard objectives add ally.golem.PosZ dummy {"text":"ally.golem: ゴーレムのZ座標"}

# Romの初期化
    function rom:init

# フォースロード
    forceload add 0 0

# ゲームルール定義
    gamerule keep_inventory false
    gamerule spectators_generate_chunks true
    gamerule max_command_sequence_length 2147483647
    gamerule advance_time false
    gamerule immediate_respawn true
    gamerule locator_bar false
    gamerule respawn_radius 0
    gamerule natural_health_regeneration false
    gamerule reduced_debug_info true
    gamerule players_nether_portal_default_delay 2147483647
    gamerule spawn_mobs false

# チーム作成と設定
    # 赤チーム
        team add Team.Red
        team modify Team.Red displayName {"color":"red","text":"赤チーム","underlined":true}
        team modify Team.Red prefix [{"color":"red","text":"[Red] "}]
        team modify Team.Red seeFriendlyInvisibles true
        team modify Team.Red nametagVisibility hideForOtherTeams
        team modify Team.Red friendlyFire false
        team modify Team.Red collisionRule pushOtherTeams
        team modify Team.Red color red
    # 青チーム
        team add Team.Blue
        team modify Team.Blue displayName {"color":"aqua","text":"青チーム","underlined":true}
        team modify Team.Blue prefix [{"color":"aqua","text":"[Blue] "}]
        team modify Team.Blue seeFriendlyInvisibles true
        team modify Team.Blue nametagVisibility hideForOtherTeams
        team modify Team.Blue friendlyFire false
        team modify Team.Blue collisionRule pushOtherTeams
        team modify Team.Blue color aqua
    # 観戦チーム
        team add Team.Spectator
        team modify Team.Spectator displayName {"color":"gray","text":"観戦","underlined":true}
        team modify Team.Spectator prefix [{"color":"gray","text":"[Spectator] "}]
        team modify Team.Spectator seeFriendlyInvisibles false
        team modify Team.Spectator friendlyFire false
        team modify Team.Spectator collisionRule pushOtherTeams
        team modify Team.Spectator color gray

# ロードされて数秒後に実行する
    schedule function core:load_init/deferred_execution 5s replace

# 初期ロードが終わったことをストレージに書いておく
    data modify storage world_manager: Game.Init set value true
